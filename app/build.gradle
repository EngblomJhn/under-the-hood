apply plugin: 'com.android.application'
def Properties localProps = getSigningProperties()

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        release {
            storeFile file(localProps.getProperty('RELEASE_KEYSTORE_PATH', 'fillhere'))
            storePassword localProps.getProperty('RELEASE_KEYSTORE_PW', '')
            keyAlias localProps.getProperty('RELEASE_KEY_ALIAS', '')
            keyPassword localProps.getProperty('RELEASE_KEY_PW', '')
        }
    }

    defaultConfig {
        applicationId "at.favre.app.hood.demo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionNameApp
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_REV", "\"" + getGitRev() + "\""
        buildConfigField "String", "GIT_BRANCH", "\"" + getGitBranch() + "\""
        buildConfigField "String", "GIT_DATE", "\"" + getGitCommitDate() + "\""

    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }

        releaseNoop {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }

        buildTypes.all {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.dependencies.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.dependencies.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile "com.android.support:appcompat-v7:$rootProject.ext.dependencies.support"

    testCompile "junit:junit:$rootProject.ext.dependencies.junit"

    debugCompile("at.favre.lib.hood:hood-extended:$rootProject.ext.versionNameDep")
    releaseCompile("at.favre.lib.hood:hood-extended:$rootProject.ext.versionNameDep")

    releaseNoopCompile("at.favre.lib.hood:hood-extended:$rootProject.ext.versionNameDep") {
        exclude group: 'at.favre.lib.hood', module: 'hood-core'
        releaseNoopCompile(group: 'at.favre.lib.hood', name: 'hood-core', version: rootProject.ext.versionNameDep,
                classifier: 'noop', ext: 'aar', transitive: true)
    }

//    compile project(path: ':hood-extended')
}

def String getGitRev() {
    def git_rev = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log', '-1', '--format=%H'
        standardOutput = git_rev
    }
    git_rev = git_rev.toString().trim()
    return git_rev
}

def String getGitCommitDate() {
    def git_date = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log', '-1', '--format=%cI'
        standardOutput = git_date
    }
    git_date = git_date.toString().trim()
    return git_date
}

def String getGitBranch() {
    def git_branch = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = git_branch
        ignoreExitValue = true
    }
    git_branch = git_branch.toString().trim()
    return git_branch
}

def getSigningProperties() {
    def Properties props = new Properties()
    try {
        props.load(new FileInputStream(file('../local.properties')))
    } catch (Exception e) {
    }
    return props
}