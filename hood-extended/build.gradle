import com.android.builder.core.BuilderConstants

apply plugin: 'com.android.library'
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply from: "$rootDir/gradle/jacoco-utils.gradle"
android {
    testBuildType "release"
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionNameLib

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        buildConfigField "String", "BUILD_NUMBER", "\"" + getCiBuildNumber() + "\""

        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        noop {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.name}-${version}.aar"
        }
    }
}

android.variantFilter { variant ->
    if (variant.buildType.name == BuilderConstants.DEBUG) {
        variant.setIgnore(true)
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$rootProject.ext.dependencies.androidx"
    implementation "com.google.android.material:material:$rootProject.ext.dependencies.androidx"

    api project(path: ':hood-core')

    androidTestImplementation(group: 'androidx.test.espresso', name: 'espresso-core', version: rootProject.ext.dependencies.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$rootProject.ext.dependencies.junit"
}

apply from: "$rootDir/gradle/publish.gradle"
