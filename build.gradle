apply from: "$rootDir/gradle/common-build.gradle"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'de.mobilej.unmock:UnMockPlugin:0.6.0'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    versionNameApp = "0.5.2"
    versionNameLib = "$versionNameApp"
    versionCode = getCiBuildNumber()
    versionNameDep = "0.+"


    compileSdkVersion = 25
    buildToolsVersion = "25.0.2"
    targetSdkVersion = 25
    minSdkVersion = 14
    javaVersion = JavaVersion.VERSION_1_7

    dependencies = [
            support : "25.3.1",
            espresso: "2.2.2",
            junit   : "4.12"
    ]
}

//disables Java8 javadoc lint which is way to restrictive
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

//this defines an Android compatible provided scope, see: https://sinking.in/blog/provided-scope-in-gradle/
configurations {
    provided {
        dependencies.all { dep ->
            configurations.default.exclude group: dep.group, module: dep.name
        }
    }
    compile.extendsFrom provided
}

task publishLibs(dependsOn: [':clean', 'hood-core:assemble', ':hood-core:install', ':hood-extended:install', ':hood-core:bintrayUpload']) {
    doLast {
        println "published"
    }
}